[import [render] "theme"]

[defn get-out-path [ctx path]
      [cat [tree/ref ctx :deploy-dir]
           [string/cut path [length [tree/ref ctx :content-root-dir]]]]]

[defn mkdir-safe [full-path]
      [def parts [split full-path "/"]]
      [def path #nil]
      [while parts
        [set! path [if path
                       [cat path "/" [car parts]]
                       [car parts]]]
        [cdr! parts]
        [mkdir path]]]

[defn build-html [ctx path]
      "Loader that just copies the file over"
      [def dest-path [get-out-path ctx path]]
      [mkdir-safe [path/dirname dest-path]]
      [spit dest-path [render ctx path]]
      [return dest-path]]

[defn build-copy [ctx path]
      "Loader that just copies the file over"
      [def dest-path [get-out-path ctx path]]
      [mkdir-safe [path/dirname dest-path]]
      [file/copy path dest-path]
      dest-path]

[defn build [ctx path]
      "Build a certain path using the loader"
      :export
      [def ext [string->keyword [lowercase [path/extension path]]]]
      [if [== ext :html]
          [build-html ctx path]
          [build-copy ctx path]]]
