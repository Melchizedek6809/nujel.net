[def loaders @[]]

[defn add-loader [extension handler]
      "Add a loader for a specific file extension"
      :export
      [tree/set! loaders extension handler]]

[defn init [ctx]
      [require "loader/copy"]
      [require "loader/html-content"]
      [return ctx]]

[defn add-default [ctx]
      "Add the default loaders to the CTX"
      :export
      [init ctx]
      [def ctx-loaders [tree/ref ctx :loaders]]
      [doseq [ext [tree/keys loaders] ctx]
             [tree/set! ctx-loaders ext [tree/ref loaders ext]]]]

[defn build [ctx path]
      "Build a certain path using the loader"
      :export
      [def ctx-loaders [tree/ref ctx :loaders]]
      [def ext [string->keyword [lowercase [path/extension path]]]]
      [[or [tree/ref ctx-loaders ext]
           [tree/ref ctx-loaders :otherwise]] ctx path]]
